buildscript {
    repositories {
        maven { url = 'https://jitpack.io' }
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/maven' }
    }
    dependencies {
        classpath 'com.github.GregTechCE:ForgeGradle:FG_2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.

version = "3.1"
group = "zone.rong.loliasm" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "loliasm"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
	version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "stable_39"
    def args = [
            '-Dfml.coreMods.load=zone.rong.loliasm.core.LoliLoadingPlugin',
            '-Dmixin.hotSwap=true',
            '-Dmixin.checks.interfaces=true',
			'-Dmixin.debug.export=true'
    ]
    clientJvmArgs.addAll(args)
    serverJvmArgs.addAll(args)
    useDepAts = true
    makeObfSourceJar = false
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

repositories {
    jcenter()
    maven {
        url "http://chickenbones.net/maven"
    }
    maven {
        url "http://maven.covers1624.net"
    }
    maven {
        url "https://www.cursemaven.com"
    }
    maven {
        url "https://repo.spongepowered.org/maven"
    }
    maven {
        url "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        url "https://modmaven.k-4u.nl/"
    }
    maven {
        url "https://maven.blamejared.com"
    }
    maven {
        url "https://maven.thiakil.com"
    }
    maven {
        url "https://repo.codemc.io/repository/maven-public/"
    }
    maven {
        url 'http://dvs1.progwml6.com/files/maven'
    }
    // maven {
        // url = "http://maven.bluexin.be/repository/snapshots/"
    // }
}

dependencies {
    embed "me.nallar.whocalled:WhoCalled:1.1"
    // implementation ("org.spongepowered:mixin:0.8.3-SNAPSHOT") {
    embed ("org.spongepowered:mixin:0.8") {
        exclude module: "asm-commons"
        exclude module: "asm-tree"
        exclude module: "launchwrapper"
        exclude module: "guava"
        exclude module: "log4j-core"
        exclude module: "gson"
        exclude module: "commons-io"
    }
    compile "codechicken:ChickenASM:1.12-1.0.2.9"
    compile "epicsquid.mysticallib:mysticallib:1.12.2-+"

    deobfCompile 'slimeknights.mantle:Mantle:1.12-1.3.3.55+'
    deobfCompile 'slimeknights:TConstruct:1.12.2-2.13.0.183+'

    deobfCompile "betterwithmods:BetterWithMods:1.12-2.3.20-1030"

    deobfCompile "codechicken:CodeChickenLib:1.12.2-3.1.7.+:universal"
    deobfCompile "com.azanor.baubles:Baubles:1.12-1.5.2"
    deobfCompile "blusunrize:ImmersiveEngineering:0.12-92-+"
    deobfCompile "curse.maven:astral-sorcery-241721:3044416"
    deobfCompile "curse.maven:tfcraft-302973:3268988"
    deobfCompile "curse.maven:mixin-booter-419286:3321174"
    deobfCompile "curse.maven:foamfix-optimization-mod-278494:3327893"

    compile "mezz.jei:jei_1.12.2:4.15.0.293"
    // deobfCompile "cofh:CoFHCore:1.12.2-+:universal"
    // compile "com.teamwizardry.librarianlib:librarianlib-1.12:4.0-SNAPSHOT:deobf"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

sourceSets {
    main {
        ext.refMap = "mixins.loliasm.refmap.json"
    }
}

jar {
    from (configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'LICENSE.txt', 'META-INF/MANIFSET.MF', 'META-INF/maven/**', 'META-INF/*.RSA', 'META-INF/*.SF'
    }
    manifest {
        attributes([
                "FMLCorePluginContainsFMLMod": true,
                "FMLCorePlugin": 'zone.rong.loliasm.core.LoliLoadingPlugin',
                "ForceLoadAsMod": true,
                "TweakClass": 'org.spongepowered.asm.launch.MixinTweaker',
                "FMLAT": "loliasm_at.cfg"
        ])
    }
}
