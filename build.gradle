buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


version = "1.0"
group = "zone.rong.loliasm" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "loliasm"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
	version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "stable_39"
    def args = [
            '-Dfml.coreMods.load=zone.rong.loliasm.LoliLoadingPlugin',
            '-Dmixin.hotSwap=true',
            '-Dmixin.checks.interfaces=true',
			'-Dmixin.debug.export=true'
    ]
    clientJvmArgs.addAll(args)
    serverJvmArgs.addAll(args)
    useDepAts = true
    makeObfSourceJar = false
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

repositories {

    jcenter()

    maven {
        url "https://www.cursemaven.com"
    }

}

dependencies {

    embed 'me.nallar.whocalled:WhoCalled:1.1'

    deobfCompile "curse.maven:forestry-59751:2918418"

}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    from (configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'LICENSE.txt', 'META-INF/MANIFSET.MF', 'META-INF/maven/**', 'META-INF/*.RSA', 'META-INF/*.SF'
    }
    manifest {
        attributes([
                "FMLCorePluginContainsFMLMod": true,
                "FMLCorePlugin": 'zone.rong.loliasm.LoliLoadingPlugin',
                "ForceLoadAsMod": true
        ])
    }
}
